[[sec:interfaces]]
== Interfaces


Interfaces in ABS are similar to interfaces in Java.  They have a name, which
defines a nominal type, and they can extend one or more interfaces.  If no
explicit `extends` clause is given, the interface will extend the standard
library interface `ABS.StdLib.Object` (see <<type-object>>).  The
`ABS.StdLib.Object` interface does not specify any methods.

The interface body consists of a list of method signature declarations.
Method names start with a lowercase letter.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _InterfaceDecl_ ::= | `interface` _SimpleTypeIdentifier_ [ `extends` _InterfaceList_ ] `{` { _MethSig_ } `}`
| _InterfaceList_ ::= | _TypeIdentifier_ { `,` _TypeIdentifier_ }
| _MethSig_ ::= | _Type_ _SimpleIdentifier_ `(` [ _Type_ _SimpleIdentifier_ { `,` _Type_ _SimpleIdentifier_ } ] `)` `;`
|====

If a method declarations in an interface has an `[Atomic]` annotation, the
compiler will statically check that any definitions for this method contain no
suspension points (`suspend` and `await` statements), i.e., that running that
method will complete without scheduling.

NOTE: For ease of reasoning and analysis, ABS methods differ only by name.  It
is an error for an interface to declare two methods with the same name, either
explicitly or via extending another interface.

The interfaces in the example below represent a database system, providing
functionality to store and retrieve files, and a node of a peer-to-peer file
sharing system.  Each node of a peer-to-peer system plays both the role of a
server and a client.

[source]
.Example
----
interface DB {
  File getFile(Filename fId);
  Int getLength(Filename fId);
  Unit storeFile(Filename fId, File file);
  Filenames listFiles();
}
interface Client {
  List<Pair<Server,Filenames>> availFiles(List<Server> sList);

  Unit reqFile(Server sId, Filename fId);
}
interface Server {
  Filenames inquire();
  Int getLength(Filename fId);
  Packet getPack(Filename fId, Int pNbr);
}
interface Peer extends Client, Server {
  List<Server> getNeighbors();
}
----

[[sec:classes]]
== Classes

Classes in ABS describe stateful behavior.  All objects in ABS have exactly
one class; objects are created via the `new` expression.

Classes implement one or more interfaces.  If a class implements more than
one interface, <<typecheck-expression>> and <<typecast-expression>> are used
to obtain a reference to the same object typed with a different interface.  If
no explicit `implements` clause is given, the class will extend the standard
library interface `ABS.StdLib.Object` (see <<type-object>>).  The
`ABS.StdLib.Object` interface does not specify any methods.

NOTE: Classes typically explicitly implement one or more interfaces so that
methods can be called on them, but the `run` method makes it meaningful to
have objects without public methods; such objects cannot accept method calls
but can send method calls to other objects.

NOTE: For ease of reasoning and analysis, ABS methods differ only by name.  It
is an error for a class to implement two interfaces that both contain a method
with the same name.

Classes have _fields_ that define the state of objects of that class.  All
fields are private and can only be accessed from methods defined in the class.
Fields are defined in two ways:

- _Field declarations_ in the body of the class define fields that get their
  initial value from their init expression.
- _Class parameters_ define fields that get their initial value from the `new`
  expression.

Classes have an optional _init block_, which is executed for each new object
before any other code.  The init block cannot contain processor release points
(i.e., `await` or `suspend`), blocking expressions (i.e., `get`, `duration`,
using resources), or explicitly throw an exception via the `throw` statement.
It is mainly used for complicated field initializations and is frequently
omitted.

Classes have an optional _run method_ (see <<sec:active-classes>>).

Classes have an optional _recovery block_.  In case an uncaught exception
occurs in a method, the exception is matched against the patterns given in the
recovery block, and the associated statement(s) are executed.  If the
exception does not match any pattern in the recovery block, or if the recovery
block itself raises an exception, the object is killed.  Code in the recovery
block has the same restrictions as in the init block (i.e., no processor
release points, blocking expressions and `throw` statements).


[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _ClassDecl_ ::= | `class` _SimpleTypeIdentifier_ [ `(` [ _ClassParameterList_ ] `)` ] [ `implements` _InterfaceList_ ] +
                    `{` [ _FieldDeclList_ ] [ _Block_ ] [_RecoveryBlock_] [ _TraitUseList_ ] { _MethDecl_ } `}`
| _ClassParameterList_ ::= | _Type_ _SimpleIdentifier_ { `,` _Type_ _SimpleIdentifier_ }
| _InterfaceList_ ::= | _TypeIdentifier_ { `,` _TypeIdentifier_ }
| _TraitUseList_ ::= | adds _TraitName_ `;` {  adds _TraitName_ `;`}
| _FieldDeclList_ ::= | { _Type_ _SimpleIdentifier_ [ `=` _PureExp_ ] `;` }
| _RecoveryBlock_ ::= | `recover` `{` { _Pattern_ `\=>` _Stmt_ } `}`
| _MethDecl_ ::= | _Type_ _SimpleIdentifier_ `(` [ _Type_ _SimpleIdentifier_ { `,` _Type_ _SimpleIdentifier_ } ] `)` _Block_
|====


A class definition contains zero or more method definitions.  Each method has
a name, return type and zero or more parameters.  All methods declared in an
interface that is implemented by the class or one of their super-interfaces
must be defined in the class body or in one of its traits.  A class is free to
define methods not declared in an interface; such methods are private to the
class and cannot be called from outside the class.

NOTE: ABS currently does not support method overloading.  Each method must
have a unique name since methods are not disambiguated by their parameter
lists.


[source]
.Example
----
class DataBase(Map<Filename,File> db) implements DB {
	File getFile(Filename fId) {
		return lookup(db, fId);
	}

	Int getLength(Filename fId){
		return length(lookup(db, fId));
	}

	Unit storeFile(Filename fId, File file) {
		db = insert(Pair(fId,file), db);
	}

	Filenames listFiles() {
		return keys(db);
	}

	}

class Node(DB db, Peer admin, Filename file) implements Peer {

	Catalog catalog;
	List<Server> myNeighbors;
	// implementation...

}

----

=== Atomic Methods

Definitions for atomic method declarations have to be annotated with
`[Atomic]` as well.  The compiler will statically check that the definition
does not contain suspension points (`suspend` and `await` statements) and
blocking `get` expressions.  Such methods can be called inside init blocks and
in `finally` clauses; all other methods cannot be called in these places.

The following example shows a call to an atomic method from an init block.
Removing the `Atomic` annotation from method `m` would lead to a compile-time
error.

[source]
.Example
----
class Sample {
    Int field = 12;

    {
        field = this.m();
    }

    [Atomic] Int m() {
        return 24;
    }
}
----


=== Constant Fields

Similar to variable declarations, field declarations and class parameters can
carry a `Final` annotation.  the effect of such an annotation is to forbid
re-assignment to such a field.

The following example will lead to compile-time errors since we are trying to
assign new values to two fields declared as `Final`:

[source]
.Example
----
class Sample ([Final] Int constant_i) {
    [Final] Int constant_j = 24;
    Unit m() {
        constant_i = 25; // error
        constant_j = 24; // error
    }
}
----

In addition to fields, method parameters and variables can also be declared
`Final`.

[[sec:active-classes]]
=== Active Classes

A class can be active or passive. Active classes start an activity on their own upon creation. Passive classes only react to incoming method calls. A class is active if and only if it has a run method:

[source]
----
Unit run() {
	// active behavior ...
}
----

The run method is asynchronously called after object initialization.

[[sec:traits]]
== Traits

ABS does not support inheritance for code reuse.  Method implementations that
are common between classes can be defined once and used inside these classes by
using _traits_.  A trait can add, remove and modify methods of a class or of
another trait.

Traits are applied to classes at compile-time and do not change the
interface(s) of a class.  Classes and their methods are type-checked once all
traits are applied.

Similar to classes, traits in ABS are not types.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _TraitDecl_ ::= | `trait` _TraitName_ = ( `{` { _MethDecl_ } `}` {vbar} _TraitName_ ) { _TraitOper_ }
| _TraitName_ ::= | _SimpleIdentifier_
| _TraitOper_ ::= | `adds` _TraitExpr_ +
{vbar} `modifies` _TraitExpr_ +
{vbar} `removes` _MethSig_
|====


A trait is defined with `trait` t = _T_ at module level.

The effect of applying a trait _T_ to a class (using `uses` _T_ inside the
class body) is to add the methods in that trait to the class
definition.

.Explanation
* The operation `adds` adds all the elements of the next _MethodSet_ to the class. 
If a method with the same name is already present in the class (or set of methods), the error will be raised _after_ applying all traits, during type checking.
[source]
----
trait T = { Unit x(){ skip; } } 
trait T2 = { Unit y(){ skip; } } adds T
----
will be resolved to the set
[source]
----
{ Unit x(){ skip; } Unit y(){ skip; } } 
----
* The operation `modifies` changes all the elements of the next _MethodSet_ in the class to the new implementation described in this _MethodSet_. 
A trait may contain original() calls which refer to the version of the method before the trait application.
If a method with the same name is not present in the class (or set of methods), the method is added instead and the original() calls are not resolved.

A method may contain targeted original calls. These raise an error if the trait is used directly by a class
The following is invalid:
[source]
.Example
----
trait T = {Unit myMethod(){ skip; }}  modifies {Unit myMethod(){ core.original(); }}
class C {uses T; }
----
The following two examples are valid:
[source]
.Example
----
trait T = {Unit myMethod(){ skip; }} modifies {Unit myMethod(){ original(); }}
class C {uses T; }
----
[source]
.Example
----
module M;
trait T = {Unit myMethod(){ skip; }} 
class C {uses T; }

delta D;
modifies class M.C{
        modifies Unit myMethod(){ core.original(); }
}
----
* The operation `removes` deletes the method with the provided signature. 
If a method with the same name is not present in the class (or set of methods), an error will be raised during trait application.

.The order of trait application is as follows:
* All traits used within a class, in the order they are referred to
* All traits used within a delta, in the order they are referred to

[source]
.Example
----
module M;
interface I { Unit x(); Unit foo(); Unit bar(); }
trait T = Unit x() { this.foo(); original(); this.bar();  }
trait T2 = { Unit x() { println("T2"); } } modifies T
trait T3 = { Unit x() { println("T3"); } } modifies T
class C implements I {
        Int i = 0;
        uses T2;
        Unit foo(){ i = i+1; }
        Unit bar(){ i = i-1; }
}

class C2 implements I {
        Int i = 0;
        uses T3;
        Unit foo(){ i = i-1; }
        Unit bar(){ i = i+1; }
}
----
